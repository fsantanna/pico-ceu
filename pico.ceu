`pico_init(1);`
defer {
    `pico_init(0);`
}

;; DATA / ENUM

data :XY = [x,y]
data :WH = [w,h]
data :Rect = [x,y,w,h]

data :Pico = [] {
    :Any   = []
    :Quit  = []
    :Frame = [ms]
    :Draw  = []
    :Key = [key] {
        :Dn = []
        :Up = []
    }
    :Mouse = [pos:XY] {
        :Motion = []
        :Button = [but] {
            :Dn = []
            :Up = []
        }
    }
}

enum {
    :HAnchor-Left  =`Left`,   :HAnchor-Center, :HAnchor-Right,
    :VAnchor-Bottom=`Bottom`, :VAnchor-Middle, :VAnchor-Top,
}

enum {
    :Mouse-Button-Left = `SDL_BUTTON_LEFT`,
    :Mouse-Button-Middle, :Mouse-Button-Right,
}

enum {
    :Key-A = `SDLK_a`,
    :Key-B, :Key-C, :Key-D, :Key-E, :Key-F, :Key-G, :Key-H, :Key-I, :Key-J,
    :Key-K, :Key-L, :Key-M, :Key-N, :Key-O, :Key-P, :Key-Q, :Key-R, :Key-S,
    :Key-T ,:Key-U, :Key-V, :Key-W, :Key-X, :Key-Y, :Key-Z,

    :Key-Right = `SDLK_RIGHT`,
    :Key-Left, :Key-Down, :Key-Up,

	:Key-Escape = `SDLK_ESCAPE`,
}

;; INPUT

var pico-input-delay = func (ms) {
    `pico_input_delay($ms.Number);`
}

var pico-event-sdl-ceu = func (sdl, type) {
    var tp = :nil
    var t0 = :nil
    var t1 = :nil
    var t2 = :nil
    var n0 = 0
    var n1 = 0
    var n2 = 0
    ```
        SDL_Event* evt = (SDL_Event*) $sdl.Pointer;
        $tp.Tag = evt->type;
        switch (evt->type) {
            case SDL_KEYDOWN:
            case SDL_KEYUP:
                $t0.Tag  = evt->key.state;
                $t1.Tag  = evt->key.keysym.sym;
                break;
            case SDL_MOUSEBUTTONDOWN:
            case SDL_MOUSEBUTTONUP:
                $n0.Number = evt->button.x;
                $n1.Number = evt->button.y;
                $t2.Tag    = evt->button.button;
                break;
            case SDL_MOUSEMOTION:
                $n0.Number = evt->button.x;
                $n1.Number = evt->button.y;
                break;
        }
    ```
    ifs tp {
        is `:tag SDL_QUIT`            -> :Pico.Quit []
        is `:tag SDL_MOUSEMOTION`     -> :Pico.Mouse.Motion [[n0,n1]]
        is `:tag SDL_MOUSEBUTTONDOWN` -> :Pico.Mouse.Button.Dn [[n0,n1],t2]
        is `:tag SDL_MOUSEBUTTONUP`   -> :Pico.Mouse.Button.Up [[n0,n1],t2]
        is `:tag SDL_KEYDOWN`         -> :Pico.Key.Dn [t1]
        is `:tag SDL_KEYUP`           -> :Pico.Key.Up [t1]
        else -> throw(:error)
    }
}

var pico-input-event = func (tag) {
    var sdl = `:pointer NULL`
    ```
        int tag_to_type (int tag) {
            switch (tag) {
                case CEU_TAG_Pico_Any:
                    return SDL_ANY;
                case CEU_TAG_Pico_Quit:
                    return SDL_QUIT;
                case CEU_TAG_Pico_Key_Dn:
                    return SDL_KEYDOWN;
                case CEU_TAG_Pico_Key_Up:
                    return SDL_KEYUP;
                case CEU_TAG_Pico_Mouse_Motion:
                    return SDL_MOUSEMOTION;
                case CEU_TAG_Pico_Mouse_Button_Dn:
                    return SDL_MOUSEBUTTONDOWN;
                case CEU_TAG_Pico_Mouse_Button_Up:
                    return SDL_MOUSEBUTTONUP;
                default:
                    assert(0 && "bug found : missing tag -> type");
            }
        }

        SDL_Event sdl;
        int type = tag_to_type($tag.Tag);
        pico_input_event(&sdl,type);
        $sdl.Pointer = &sdl;
    ```
    pico-event-sdl-ceu(sdl, type)
}

var pico-input-event-timeout = func (type, time) {
    var sdl = `:pointer NULL`
    var ok = false
    ```
        SDL_Event sdl;
        $ok.Bool = pico_input_event_timeout(&sdl,$type.Tag,$time.Number);
        $sdl.Pointer = &sdl;
    ```
    ok and pico-event-sdl-ceu(sdl, type)
}

;; OUTPUT

var pico-output-clear = func () {
    `pico_output_clear();`
}

var pico-output-draw-image = func (x,y,img,cache) {
    set cache = cache or (cache == nil)
    `_pico_output_draw_image_cache((SDL_Point){$x.Number,$y.Number}, (char*)$img.Dyn->Ncast.Vector.buf, $cache.Bool);`
}

var pico-output-draw-line = func (p1,p2) {
    var x1 = p1[0]
    var y1 = p1[1]
    var x2 = p2[0]
    var y2 = p2[1]
    `pico_output_draw_line((SDL_Point){$x1.Number,$y1.Number},
                           (SDL_Point){$x2.Number,$y2.Number});`
}

var pico-output-draw-rect = func (rct) {
    var x = rct[0]
    var y = rct[1]
    var w = rct[2]
    var h = rct[3]
    `pico_output_draw_rect((SDL_Rect){$x.Number,$y.Number,$w.Number,$h.Number});`
}

var pico-output-draw-pixel = func (x,y) {
    `pico_output_draw_pixel((SDL_Point){$x.Number,$y.Number});`
}

var pico-output-draw-text = func (x,y,txt) {
    `pico_output_draw_text((SDL_Point){$x.Number,$y.Number}, $txt.Dyn->Ncast.Vector.buf);`
}

var pico-output-present = func () {
    `pico_output_present();`
}

var pico-output-write = func (txt) {
    `pico_output_write($txt.Dyn->Ncast.Vector.buf);`
}

var pico-output-writeln = func (txt) {
    `pico_output_writeln($txt.Dyn->Ncast.Vector.buf);`
}

;; STATE

;; GET

var pico-state-get-size = func () {
    var w = 0
    var h = 0
    ```
        SDL_Point sz;
        pico_state_get_size(&sz);
        $w.Number = sz.x;
        $h.Number = sz.y;
    ```
    [w,h]
}

var pico-state-get-size-image = func (img) {
    var w = 0
    var h = 0
    ```
        SDL_Point sz;
        pico_state_get_size_image((char*)$img.Dyn->Ncast.Vector.buf, &sz);
        $w.Number = sz.x;
        $h.Number = sz.y;
    ```
    [w,h]
}

;; SET

var pico-state-set-anchor = func (h,v) {
    `pico_state_set_anchor($h.Tag,$v.Tag);`
}

var pico-state-set-auto = func (on) {
    `pico_state_set_auto($on.Bool);`
}

var pico-state-set-color-clear = func (r,g,b,a) {
    `pico_state_set_color_clear((SDL_Color){$r.Number,$g.Number,$b.Number,$a.Number});`
}

var pico-state-set-color-draw = func (r,g,b,a) {
    `pico_state_set_color_draw((SDL_Color){$r.Number,$g.Number,$b.Number,$a.Number});`
}

var pico-state-set-cursor = func (x,y) {
    `pico_state_set_cursor((SDL_Point){$x.Number,$y.Number});`
}

var pico-state-set-font = func (file, h) {
    `pico_state_set_font($file.Dyn->Ncast.Vector.buf,$h.Number);`
}

var pico-state-set-grid = func (on) {
    `pico_state_set_grid($on.Bool);`
}

var pico-state-set-image-crop = func (x,y,w,h) {
    if x == nil {
        `pico_state_set_image_crop((SDL_Rect){0,0,0,0});`
    } else {
        `pico_state_set_image_crop((SDL_Rect){$x.Number,$y.Number,$w.Number,$h.Number});`
    }
}

var pico-state-set-pan = func (x,y) {
    `pico_state_set_pan((SDL_Point){$x.Number,$y.Number});`
}

var pico-state-set-size = func (w,h) {
    `pico_state_set_size((SDL_Point){$w.Number,$h.Number});`
}

var pico-state-set-size-image = func (w,h) {
    `pico_state_set_size_image((SDL_Point){$w.Number,$h.Number});`
}

var pico-state-set-size-pixel = func (w,h) {
    `pico_state_set_size_pixel((SDL_Point){$w.Number,$h.Number});`
}

var pico-state-set-title = func (title) {
    `pico_state_set_title($title.Dyn->Ncast.Vector.buf);`
}

;; AUX

func pico-point-vs-rect? (pt :XY, r :Rect) {
    ifs {
        pt.x < (r.x-(r.w/2)) -> false
        pt.x > (r.x+(r.w/2)) -> false
        pt.y < (r.y-(r.h/2)) -> false
        pt.y > (r.y+(r.h/2)) -> false
        else                 -> true
    }
}

func pico-rect-vs-rect? (r1, r2) {
    ifs {
        pico-point-vs-rect?(p1,r1) -> true
        pico-point-vs-rect?(p2,r1) -> true
        pico-point-vs-rect?(p3,r1) -> true
        pico-point-vs-rect?(p4,r1) -> true
        else                      -> false
    } where {
        var p1 = [(r2.0-(r2.2/2)), (r2.1-(r2.3/2))]
        var p2 = [(r2.0-(r2.2/2)), (r2.1+(r2.3/2))]
        var p3 = [(r2.0+(r2.2/2)), (r2.1+(r2.3/2))]
        var p4 = [(r2.0+(r2.2/2)), (r2.1-(r2.3/2))]
    }
}

;; LOOP

var pico-loop = func () {
    pico-state-set-auto(false)
    var old
    set old = `:number SDL_GetTicks()`
    while true { {:break}
        var now
        set now = `:number SDL_GetTicks()`
        var togo
        set togo = 40 - (now - old)
        if togo <= 0 {
            broadcast in :global, :Pico.Frame [40]
            broadcast in :global, :frame [40]
            pico-output-clear()
            broadcast in :global, :Pico.Draw []
            pico-output-present()
            set old = togo + now
        } else {
            var tmp = pico-input-event-timeout(`:tag SDL_ANY`,togo)
            if tmp {
                if tmp is :Pico.Quit {
                    throw(:break)
                }
                broadcast in :global, tmp
            }
        }
    }
}
