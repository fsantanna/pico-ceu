^["@/pico/pico.ceu"]

pico-state-set-size([810,610])
pico-state-set-title("pico-Ceu: Click, Drag, or Cancel")
pico-state-set-font("tiny.ttf", 10)

spawn task () {
    var text = " "
    var rect :Rect = [[0,0],[10,10]]
    spawn task () {
        xloop {
            xloop { ;; AWAIT
                xbreak if yield(nil) { as it =>
                    is?(it, :Pico.Draw)
                }
            }
            pico-output-draw-rect(rect)
            pico-output-draw-text([0,-20], text)
        }
    } ()
    xloop {
        val click :XY = xloop { ;; AWAIT
            xbreak if yield(nil) { as it :Pico.Mouse =>
                if is?(it, :Pico.Mouse.Button.Dn) {
                    if pico-point-vs-rect?(it.pos,rect) {
                        copy(it.pos)
                    } else {
                        false
                    }
                } else {
                    false
                }
            }
        }
        set text = "... clicking ..."
        val orig  :Rect = copy(rect)

        val t1 = spawn task () {
            xloop { ;; AWAIT
                xbreak if yield(nil) { as it :Pico.Key =>
                    if is?(it, :Pico.Key.Dn) {
                        (it.key == :Key-Escape)
                    } else {
                        false
                    }
                }
            }
            set rect = copy(orig)
            set text = "!!! CANCELLED !!!"
        } ()

        val t2 = spawn task () {
            var pos :XY = xloop { ;; AWAIT
                xbreak if yield(nil) { as it :Pico.Mouse =>
                    if is?(it, :Pico.Mouse.Motion) {
                        copy(it.pos)
                    } else {
                        false
                    }
                }
            }
            set text = "... dragging ..."
            spawn task () {
                xloop {
                    set rect.pos.x = orig.pos.x + (pos.x - click.x)
                    set rect.pos.y = orig.pos.y + (pos.y - click.y)
                    set pos = xloop { ;; AWAIT
                        xbreak if yield(nil) { as it :Pico.Mouse =>
                            if is?(it, :Pico.Mouse.Motion) {
                                copy(it.pos)
                            } else {
                                false
                            }
                        }
                    }
                }
            } ()
            xloop { ;; AWAIT
                xbreak if yield(nil) { as it =>
                    is?(it, :Pico.Mouse.Button.Up)
                }
            }
            set text = "!!! DRAGGED !!!"
        } ()

        val t3 = spawn task () {
            xloop { ;; AWAIT
                xbreak if yield(nil) { as it =>
                    is?(it, :Pico.Mouse.Button.Up)
                }
            }
            set text = "!!! CLICKED !!!"
        } ()

        xloop { ;; AWAIT
            xbreak if yield(nil) { as it =>
                (it==t1) || (it==t2) || (it==t3)
            }
        }
    }
} ()

xloop {
    val e = resume pico-coro ()
    xbreak if do {
        if e {
            broadcast(e)
            is?(e, :Pico.Quit)
        } else {
            nil
        }
    }
}
