^"int.ceu"
^"pico.ceu"

var rect: Rect = [[_10,_10],[_5,_5]]

spawn {
    loop {
        var dxy: Size
        {
            var mouse: Point
            await evt?UMouseButtonDown until isPointVsRect [mouse,rect]
                where {
                    set mouse = evt!UMouseButtonDown.pos
                }
            set dxy = [sub [rect.pos.x,mouse.x], sub [rect.pos.y,mouse.y]]
        }
        paror {
            var orig = rect
            await evt?UKeyDown until eq [evt!UKeyDown,_SDLK_ESCAPE]
            set rect = orig
            output std _("Cancelled!"):_(char*)
        } with {
            await evt?UMouseMotion
            output std _("Dragging..."):_(char*)
            var mouse = evt!UMouseMotion
            watching evt?UMouseButtonUp {
                loop {
                    set rect = [pt, rect.size]
                        where {
                            var pt: Point = [add [mouse.x,dxy.w], add [mouse.y,dxy.h]]
                        }
                    await evt?UMouseMotion
                    set mouse = evt!UMouseMotion
                }
            }
            output std _("Dropped!"):_(char*)
        } with {
            await evt?UMouseButtonUp
            output std _("Clicked!"):_(char*)
        }
    }
}

spawn {
    loop {
        await _1
        output pico TPico.UClear
        output pico TPico.UDraw_Rect rect
        output pico TPico.UPresent
    }
}

call pico_loop ()
